;
;
;
; Newbrain chess, version 2
; Derived from n17chess
;

	ORG 13769
; substitute 13769 for 2100H
;call 2100H...encodes program
;call 2103H...saves program
;call 2106H...tests display
;call 2109H...plays game
;
YES	EQU 1
NO	EQU 0
;
RANGE	EQU 6
PRUNNO	EQU 2
NEWVAL	EQU YES
EXTRA	EQU YES
;
;
PAWN	EQU 1
KNIGHT	EQU 2
BISHOP	EQU 3
ROOK	EQU 4
QUEEN	EQU 5
KING	EQU 6
WHITE	EQU 0
BLACK	EQU 80H
BPAWN	EQU BLACK+PAWN
CLOCK	EQU 0052H
BOTLIN	EQU 255

VCODE:	CALL	ENCODE
VSAVE:	JP SAVEMC
VTSTBD:	JP TESTBD
;
DRIVER:	CALL	INITV
	CALL INITBD
	RST 32
	DB 37			;zinitrnd
	CALL NEWCHR
	CALL CLRSCR
	CALL CHEKER
DRIV05:	LD DE,SUPMSG
	LD A,SUPLEN
	CALL PRTBLK
	CALL CARRET
	LD DE,CPSMSG
	LD A,CPSLEN
	CALL PRTBLK
	CALL CARRET
	LD DE,CWMSG
	LD A,CWLEN
	CALL PRTBLK
	CALL CARRET
DRIV01:	LD DE,ANAMSG
	LD A,ANALEN
	CALL PRTBLK
	CALL CHARTR
	CALL CARRET
	CP 'A'
	JP Z,ANALYS
	SUB A
	LD (COLOR),A
	CALL INTERR
	CALL CLRSCR
	CALL INITBD
	LD A,1
	LD (MOVENO),A
	LD (LINECT),A
	LD HL,MVENUM
	LD (HL),'0'
	INC HL
	LD (HL),'1'
	INC HL
	LD (HL),20H
	CALL DSPBRD
	CALL PRTTL
DRIV04:	CALL PRMVNM
	JR NZ,DR08
	CALL PGIFND
	CP 1
	CALL Z,TBCPCL
	CALL CPTRMV
	LD A,1
	CALL SPACES
	CALL PLYRMV
	CALL CARRET
	JR DR0C
DR08:	CALL PLYRMV
	LD A,1
	CALL SPACES
	CALL PGIFND
	CP 1
	CALL Z,TBCPCL
	CALL CPTRMV
	CALL CARRET
DR0C:	LD HL,MVENUM+2
	LD A,20H
	CP (HL)
	LD A,3AH
	JR Z,DR10
	INC (HL)
	CP (HL)
	JR NZ,DR14
	LD (HL),'0'
DR10:	DEC HL
	INC (HL)
	CP (HL)
	JR NZ,DR14
	LD (HL),'0'
	DEC HL
	INC (HL)
	CP (HL)
	JR NZ,DR14
	LD (HL),'1'
	LD A,'0'
	LD (MVENUM+2),A
DR14:	LD HL,MOVENO
	INC (HL)
	JP DRIV04
TESTBD:	CALL NEWCHR
	CALL CLRSCR
	CALL CHEKER
	CALL INITBD
	CALL DSPBRD
	RET
ENABLE:	PUSH AF
	PUSH DE
	LD A,(FAST)
	AND A
	JR Z,EB10
EB10:	POP DE
	POP AF
	RET
;
DISABL:	PUSH AF
	PUSH DE
	LD A,(FAST)
	AND A
	JR Z,DBL10
	LD E,2
	LD A,0
	RST 32
	DB 48
DBL10:	POP DE
	POP AF
	RET
BKSP:	LD A,8
	CALL PRTCHR
	LD A,20H
	CALL PRTCHR
	LD A,8
	CALL PRTCHR
	RET
LETV1:	CALL BKSP
LETVAL:	CALL CHARTR
	CP 'R'
	JP Z,RECMOV
	CP 'X'
	JR Z,RESINE
;
	CP 'A'
;
	JR C,LETV1
	CP 'I'
	JR NC,LETV1
	RET
NUMV1:	CALL BKSP
NUMVAL:	CALL CHARTR
	CP 12H
	JR Z,RESINE
	CP '1'
	JR C,NUMV1
	CP '9'
	JR NC,NUMV1
	RET
;
RESINE:	POP HL
	JP FCM09
;
RECMOV:	LD A,(BOKFL)
	CP 1
	JR NZ,REC10
	POP AF			
	JP PL08
REC10:	CALL BKSP
	LD A,(QPLY)
	LD (TT1),A
	LD A,1
	LD (QPLY),A
	LD A,(KOLOR)
	XOR 80H
	LD (COLOR),A
	CALL FNDMOV
	LD HL,(BESTM)
	LD (MLPTRJ),HL
	CALL EXECMV
	PUSH DE
	LD D,C
	CALL BITASN
	LD A,L
	CALL PRTCHR
	LD A,H
	CALL PRTCHR
	LD A,'-'
	CALL PRTCHR
	POP DE
	LD D,E
	CALL BITASN
	LD A,L
	CALL PRTCHR
	LD A,H
	CALL PRTCHR
	LD B,5
	LD A,8
REC20:	CALL PRTCHR
	DJNZ REC20
	LD A,(TT1)
	LD (QPLY),A
	JP LETVAL
DRAWN:	CALL CARRET
	LD DE,STMSG
	LD A,9
	CALL PRTLIN
	JP FCM08
CPTRMV:	LD A,(KOLOR)
	LD (COLOR),A
	CALL FNDMOV
	LD HL,(BESTM)
	LD (MLPTRJ),HL
	LD A,(SCORE+1)
	CP 1
	JR NZ,CP0A
	LD C,1
	CALL FCDMAT
CP0A:	LD A,(STALEF)
	AND A
	CALL Z,DRAWN
CP0C:	CALL BEEP
;
CALL ENCODE
;
	CALL MOVE
;
CALL ENCODE
;
	CALL EXECMV
	LD A,B
	AND A
	JR NZ,CP10
	PUSH DE
	LD D,C
	CALL BITASN
	LD A,L
	CALL PRTCHR
	LD A,H
	CALL PRTCHR
	LD A,'-'
	CALL PRTCHR
	POP DE
	LD D,E
	CALL BITASN
	LD A,L
	CALL PRTCHR
	LD A,H
	CALL PRTCHR
	JR CP1C
CP10:	BIT 1,B
	JR Z,CP11
	LD DE,OO
	LD A,5
	CALL PRTBLK
	JR CP1C
CP11:	BIT 2,B
	JR Z,CP12
	LD DE,OOO
	LD A,5
	CALL PRTBLK
	JR CP1C
CP12:	LD DE,PXPEP
	LD A,5
	CALL PRTBLK
;
CP1C:	PUSH BC
	CALL ENCODE
	POP BC
	LD A,(COLOR)

;
	LD B,A
	XOR 80H
	LD (COLOR),A
	CALL INCHK
;
	PUSH AF
	PUSH BC
	CALL ENCODE
	POP BC
	POP AF

;
	AND A
	LD A,B
	LD (COLOR),A
	JR Z,CP24
	CALL CARRET
	LD A,(SCORE+1)
	CP 0FFH
	CALL NZ,TBCPMV
	LD DE,CKMSG
	LD A,5
	CALL PRTBLK
	LD HL,LINECT
	INC (HL)
CP24:	LD A,(SCORE+1)
	CP 0FFH
	RET NZ
	LD C,0
	CALL FCDMAT
	RET
;
;
; INITV - initialise variables in ram
;
INITV:
; first MVENUM
	LD	HL,MVENUM
	LD	(HL),'0'
	INC	HL
	LD	(HL),'1'
	INC	HL
	LD	(HL),' '
; now MVEMSG
	INC	HL
	LD	(HL),' '
	INC	HL
	LD	(HL),' '
; now TT1
	INC	HL
	LD	(HL),0
; now BCKSTR
	INC	HL
	LD	(HL),16
	INC	HL
	LD	(HL),15
; now STRNSP
	LD	HL,STRNSP
	LD	(HL),147
	INC	HL
	LD	(HL),147
; now STRSP
	INC	HL
	LD	(HL),' '
	INC	HL
	LD	(HL),' '
; now GCSTR
	LD	HL,GCSTR
	LD	(HL),16
	INC	HL
	LD	(HL),7
; now NPLOT
	INC	HL
	LD	(HL),0
; now GPSTR
	LD	(HL),16
	INC	HL
	LD	(HL),7
; now NCHARS
	INC	HL
	LD	(HL),2
	INC	HL
	LD	(HL),0
; now SQCOL
	LD	HL,SQCOL
	LD	(HL),0
; now XPOS, YPOS
	INC	HL
	LD	(HL),0
	INC	HL
	LD	(HL),0
; now SP4
	INC	HL
	LD	(HL),32
	INC	HL
	LD	(HL),32
	INC	HL
	LD	(HL),148
; now NSP4
	INC	HL
	LD	(HL),147
	INC	HL
	LD	(HL),147
	INC	HL
	LD	(HL),147
; now FAST
	LD	HL,FAST
	LD	(HL),0
; now KLRAM
	LD	HL,KLRAM
	LD	(HL),0
	JP	INV10
; now direction tables to piece values
DTABLE:	DB 9,11,-11,-9,10,-10,1,-1,-21
	DB -12,8,19,21,12,-8,-19,10,10
	DB 11,9,-10,-10,-11,-9
;
	DB 20,16,8,0,4,0,0
;
	DB 4,4,8,4,4,8,8
;
	DB 1,4,4,6,11,12

INV10:	LD	HL,DTABLE
	LD	DE,TBASE
	LD	BC,VALPCE+5-VARS+80H
	LDIR
;
; now POSK
	LD	HL,POSK
	LD	(HL),25
	INC	HL
	LD	(HL),95
	INC	HL
	LD	(HL),24
	INC	HL
	LD	(HL),94
	INC	HL
	LD	(HL),-1
; now PIECES
	LD	HL,TBASEX-8
	LD	(HL),4
	INC	HL
	LD	(HL),2
	INC	HL
	LD	(HL),3
	INC	HL
	LD	(HL),5
	INC	HL
	LD	(HL),6
	INC	HL
	LD	(HL),3
	INC	HL
	LD	(HL),2
	INC	HL
	LD	(HL),4
; now score table
	LD	HL,SCORE-1
	LD	(HL),-1
	INC	HL
	LD	(HL),0
; now M1 etc.
	LD	HL,TBASEX
	LD	(M1),HL
	LD	(M2),HL
	LD	(M3),HL
	LD	(M4),HL
	LD	HL,TBASE
	LD	(T1),HL
	LD	(T2),HL
	LD	(T3),HL
	LD	(INDX1),HL
	LD	(INDX2),HL
	LD	(NPINS),HL
	LD	HL,PLYIX
	LD	(MLPTRI),HL
	LD	HL,MLIST
	LD	(MLNXT),HL
;
	RET
;

;
BKSPL:	CALL BKSP
	JP AN01
BKSPN:	CALL BKSP
	JP AN02
BKSPC:	CALL BKSP
	JP AN08
ANALYS:	XOR A
	LD (BOKFL),A
	CALL CLRSCR
	LD DE,SQMES
	LD A,SQLEN
	CALL PRTBLK
AN01:	CALL CHARTR
	CP 'X'
	JP Z,AN15
	CP 'A'
	JR C,BKSPL
	CP 'I'
	JR NC,BKSPL
	LD H,A
AN02:	CALL CHARTR
	CP 'X'
	JR Z,AN15
	LD L,A
	CP 'L'
	JR NZ,AN04
	LD A,H
	CP 'C'
	LD A,L
	JR NZ,AN04
	CALL CLBRD
	JP AN12
AN04:	CP '1'
	JR C,BKSPN
	CP '9'
	JR NC,BKSPN
	LD L,A
	CALL ASNTBI
	LD (BRDPOS),A
	CALL CARRET
	LD DE,PCEMES
	LD A,PCELEN
	CALL PRTBLK
AN08:	CALL CHARTR
	CALL CARRET
	LD BC,7
	LD HL,PCS
	CPIR
	JR NZ,AN10
	LD DE,PCLMES
	LD A,PCLLEN
	CALL PRTBLK
	CALL CHARTR
	CALL CARRET
	CP 'B'
	JR NZ,$+4
	SET 7,C
	LD DE,MVDMES
	LD A,MVDLEN
	CALL PRTBLK
	CALL CHARTR
	CP 'N'
	JR Z,$+4
	SET 3,C
AN10:	LD A,(BRDPOS)
	LD (M1),A
	LD IX,(M1)
	LD (IX+BOARD),C
AN12:	CALL DSPBRD
	JP ANALYS
AN15:	CALL ROYALT
	CALL CLRSCR
	CALL INTERR
AN1C:	CALL CARRET
	LD DE,WSMOVE
	LD A,WSMLEN
	CALL PRTBLK
	CALL CHARTR
	PUSH AF
	CALL DSPBRD
	CALL PRTTL
	POP AF
	CP 'W'
	JP Z,DRIV04
	LD DE,MVENUM
	LD A,3
	CALL PRTBLK
	LD A,6
	CALL SPACES
	LD A,(KOLOR)
	AND A
	JR NZ,AN20
	CALL PLYRMV
	CALL CARRET
	JP DR0C
AN20:	CALL CPTRMV
	CALL CARRET
	JP DR0C
RND:	PUSH HL
	PUSH DE
	PUSH BC
	RST 32
	DB 0			
	RST 32
	DB 41			
	BIT 0,E
	POP BC
	POP DE
	POP HL
	RET
;
ENCODE:	RET		; return for now
	LD HL,GENMOV
	LD BC,BOOK-GENMOV-1
EN10:	LD A,(HL)
	XOR C
	LD (HL),A
	INC HL
	DEC BC
	LD A,B
	OR C
	JR NZ,EN10
	LD HL,KILLER
	LD BC,ENDPRG-KILLER-1
EN20:	LD A,(HL)
	XOR C
	LD (HL),A
	INC HL
	DEC BC
	LD A,B
	OR C
	JR NZ,EN20
	RET
SAVEMC:	LD HL,VCODE
	LD BC,16*1024
	LD E,1
	RST 32
	DB 61
	LD E,1
	RST 32
	DB 52
	RET
;
;
CHEKER:	LD D,YOFF
	LD E,XOFF
	LD A,1
	LD (SQCOL),A
	LD B,8
CK10:	PUSH BC
	LD B,8
CK20:	PUSH BC
	PUSH DE
	LD A,E
	LD (XPOS),A
	LD A,D
	LD (YPOS),A
	LD A,(SQCOL)
	AND A
	JR NZ,CK30
	LD BC,3
	LD HL,NSP4
	LD DE,STR4A
	LDIR
	LD BC,3
	LD HL,NSP4
	LD DE,STR4B
	LDIR
	JP CK40
CK30:	LD BC,3
	LD HL,SP4
	LD DE,STR4A
	LDIR
	LD BC,3
	LD HL,SP4
	LD DE,STR4B
	LDIR
CK40:	LD A,18
	LD (NPLOT),A
	LD D,0
	LD A,(XPOS)
	LD E,A
	RST 32
	DB 40
	LD HL,GC1
	RST 32
	DB 45
	LD D,0
	LD A,(YPOS)
	LD E,A
	ADD 10
	LD (YPOS),A
	RST 32
	DB 40
	LD HL,GC2
	RST 32
	DB 45
	LD E,11
	LD BC,14
	LD HL,GCSTR
	RST 32
	DB 61
	LD BC,3
	LD HL,STR4A
	LD DE,CHRSTR
	LDIR
	LD A,3
	LD (NCHARS),A
	LD E,11
	LD BC,7
	LD HL,GPSTR
	RST 32
	DB 61
	LD D,0
	LD A,(XPOS)
	LD E,A
	RST 32
	DB 40
	LD HL,GC1
	RST 32
	DB 45
	LD D,0
	LD A,(YPOS)
	LD E,A
	RST 32
	DB 40
	LD HL,GC2
	RST 32
	DB 45
	LD E,11
	LD BC,14
	LD HL,GCSTR
	RST 32
	DB 61
	LD BC,3
	LD HL,STR4B
	LD DE,CHRSTR
	LDIR
	LD A,3
	LD (NCHARS),A
	LD E,11
	LD BC,7
	LD HL,GPSTR
	RST 32
	DB 61
	POP DE
	LD A,21
	ADD E
	LD E,A
	LD A,(SQCOL)
	XOR 1
	LD (SQCOL),A
	POP BC
	DEC B
	JP NZ,CK20
	LD A,(SQCOL)
	XOR 1
	LD (SQCOL),A
	LD E,XOFF
	LD A,D
	SUB 20
	LD D,A
	POP BC
	DEC B
	JP NZ,CK10
	RET



DSPBRD:	LD HL,BOARDA+91
	LD D,YOFF
	LD E,XOFF+3
	LD A,1
	LD (SQCOL),A
	LD B,8
DB10:	PUSH BC
	LD B,8
DB20:	PUSH BC
	LD A,(HL)
	PUSH HL
	LD BC,BOARDB-BOARDA
	ADD HL,BC
	CP (HL)
	LD (HL),A
	POP HL
	CALL NZ,DSPCE
	LD A,21
	ADD E
	LD E,A
	INC HL
	LD A,(SQCOL)
	XOR 1
	LD (SQCOL),A
	POP BC
	DJNZ DB20
	LD A,(SQCOL)
	XOR 1
	LD (SQCOL),A
	LD BC,18
	AND A
	SBC HL,BC
	LD E,XOFF+3
	LD A,D
	SUB 20
	LD D,A
	POP BC
	DJNZ DB10
	RET
;
;
;
;
;
;
DSPCE:	PUSH HL
	PUSH DE
	LD A,E
	LD (XPOS),A
	LD A,D
	LD (YPOS),A
	LD A,(HL)
	LD (P1),A
	AND 7
	LD (T1),A
	JR NZ,DP20
	LD A,(SQCOL)
	AND A
	JR NZ,DP10
	LD BC,2
	LD HL,STRNSP
	LD DE,STR43
	LDIR
	LD BC,2
	LD HL,STRNSP
	LD DE,STR21
	LDIR
	JP DP30
DP10:	LD BC,2
	LD HL,STRSP
	LD DE,STR21
	LDIR
	LD BC,2
	LD HL,STRSP
	LD DE,STR43
	LDIR
	JP DP30
DP20:	LD HL,SQCOL
	BIT 0,(HL)
	JR NZ,DP24
	PUSH AF
	LD DE,1
	CALL BCKGND
	POP AF
	DEC A
	LD HL,P1
	BIT 7,(HL)
	JR Z,DP25
	ADD 6
	JP DP25
DP24:	DEC A
	LD HL,P1
	BIT 7,(HL)
	JR NZ,DP25
	ADD 6
DP25:	ADD A
	ADD A
	LD HL,PDATA
	LD D,0
	LD E,A
	ADD HL,DE
	LD BC,2
	LD DE,STR43
	LDIR
	LD BC,2
	LD DE,STR21
	LDIR
DP30:	LD A,18
	LD (NPLOT),A
	LD D,0
	LD A,(XPOS)
	LD E,A
	RST 32
	DB 40
	LD HL,GC1
	RST 32
	DB 45
	LD D,0
	LD A,(YPOS)
	LD E,A
	ADD 10
	LD (YPOS),A
	RST 32
	DB 40
	LD HL,GC2
	RST 32
	DB 45
	LD E,11
	LD BC,14
	LD HL,GCSTR
	RST 32
	DB 61
	LD BC,2
	LD HL,STR43
	LD DE,CHRSTR
	LDIR
	LD A,2
	LD (NCHARS),A
	LD E,11
	LD BC,6
	LD HL,GPSTR
	RST 32
	DB 61

	LD D,0
	LD A,(XPOS)
	LD E,A
	RST 32
	DB 40
	LD HL,GC1
	RST 32
	DB 45
	LD D,0
	LD A,(YPOS)
	LD E,A
	RST 32
	DB 40
	LD HL,GC2
	RST 32
	DB 45
	LD E,11
	LD BC,14
	LD HL,GCSTR
	RST 32
	DB 61
	LD BC,2
	LD HL,STR21
	LD DE,CHRSTR
	LDIR
	LD A,2
	LD (NCHARS),A
	LD E,11
	LD BC,6
	LD HL,GPSTR
	RST 32
	DB 61
	LD DE,0
	CALL BCKGND
	POP DE
	POP HL
	RET
;
BCKGND:	RST 32
	DB 40
	LD HL,BCK2
	RST 32
	DB 45
	LD HL,BCKSTR
	LD BC,8
	LD E,11
	RST 32
	DB 61
	RET
;
;
;
PDATA:	DB 12,13,0,1	;WPAWN
	DB 14,15,2,3	;WKNIGHT
	DB 14,15,4,5	;WBISHOP
	DB 14,15,6,7	;WROOK
	DB 14,15,8,9	;WQUEEN
	DB 14,15,10,11	;WKING
;
	DB 28,29,16,17	;bpawn
	DB 30,31,18,19	;bknight
	DB 30,31,20,21	;bbishop
	DB 30,31,22,23	;brook
	DB 30,31,24,25	;bqueen
	DB 30,31,26,27	;bking
;


;
UDGDAT:	DB 0
	DB 0
	DB 00000001B
	DB 00000011B
	DB 00000111B
	DB 00000111B
	DB 00000011B
	DB 00000001B	;left hand white pawn

	DB 0
	DB 0
	DB 10000000B
	DB 11000000B
	DB 11100000B
	DB 11100000B
	DB 11000000B
	DB 10000000B	;right hand white pawn

	DB 0
	DB 0
	DB 00000001B
	DB 00111111B
	DB 01111111B
	DB 00011111B
	DB 01111111B
	DB 00000001B	;left hand white knight

	DB 00100000B
	DB 01100110B
	DB 11111100B
	DB 10011000B
	DB 10011000B
	DB 11111000B
	DB 11111000B
	DB 11100000B	;right hand white knight

	DB 0
	DB 00000111B
	DB 00001111B
	DB 00011111B
	DB 00011111B
	DB 00001111B
	DB 00000111B
	DB 00000011B	;left hand white bishop

	DB 0
	DB 11000000B
	DB 10001000B
	DB 00011000B
	DB 10111000B
	DB 11110000B
	DB 11100000B
	DB 11000000B	;right hand white bishop

	DB 0
	DB 0
	DB 0
	DB 01110001B
	DB 01110001B
	DB 01111111B
	DB 01111111B
	DB 00000111B	;LEFT HAND WHITE ROOK

	DB 0
	DB 0
	DB 0
	DB 10001110B
	DB 10001110B
	DB 11111110B
	DB 11111110B
	DB 11100000B	;RIGHT HAND WHITE ROOK

	DB 0
	DB 10101010B
	DB 11111111B
	DB 10111111B
	DB 00001111B
	DB 00000111B
	DB 00000011B
	DB 00000111B	;LEFT HAND WHITE QUEEN

	DB 0
	DB 01010101B
	DB 11111111B
	DB 11111101B
	DB 11110000B
	DB 11100000B
	DB 11000000B
	DB 11100000B	;RIGHT HAND WHITE QUEEN

	DB 0
	DB 00000001B
	DB 00001111B
	DB 00000001B
	DB 00000001B
	DB 00011111B
	DB 00001111B
	DB 00000111B	;LEFT HAND WHITE KING

	DB 0
	DB 10000000B
	DB 11110000B
	DB 10000000B
	DB 10000000B
	DB 11111000B
	DB 11110000B
	DB 11100000B	;RIGHT HAND WHITE KING


	DB 00000011B
	DB 00000011B
	DB 00000111B
	DB 00000111B
	DB 00000111B
	DB 00001111B
	DB 00001111B
	DB 0		;LEFT HAND SMALL WHITE BASE

	DB 11000000B
	DB 11000000B
	DB 11100000B
	DB 11100000B
	DB 11100000B
	DB 11110000B
	DB 11110000B
	DB 0		;RIGHT HAND SMALL WHITE BASE


	DB 00000011B
	DB 00000111B
	DB 00001111B
	DB 00000111B
	DB 00000111B
	DB 00001111B
	DB 00011111B
	DB 00111111B	;LEFT HAND LARGE WHITE BASE

	DB 11000000B
	DB 11100000B
	DB 11110000B
	DB 11100000B
	DB 11100000B
	DB 11110000B
	DB 11111000B
	DB 11111100B	;RIGHT HAND LARGE WHITE BASE

;
;
;
;
	DB 0
	DB 0
	DB 00000001B
	DB 00000010B
	DB 00000100B
	DB 00000100B
	DB 00000010B
	DB 00000001B	;LEFT HAND BLACK PAWN

	DB 0
	DB 0
	DB 10000000B
	DB 01000000B
	DB 00100000B
	DB 00100000B
	DB 01000000B
	DB 10000000B	;RIGHT HAND BLACK PAWN

	DB 0
	DB 0
	DB 00000001B
	DB 00111111B
	DB 01100000B
	DB 00010000B
	DB 01111110B
	DB 00000001B	;LEFT HAND BLACK KNIGHT

	DB 00100000B
	DB 01100110B
	DB 10011100B
	DB 01101000B
	DB 01101000B
	DB 00001000B
	DB 00011000B
	DB 00100000B	;RIGHT HAND BLACK KNIGHT

	DB 0
	DB 00000111B
	DB 00001000B
	DB 00010001B
	DB 00010000B
	DB 00001000B
	DB 00000100B
	DB 00000010B	;LEFT HAND BLACK BISHOP

	DB 0
	DB 11000000B
	DB 10001000B
	DB 00011000B
	DB 10101000B
	DB 11010000B
	DB 00100000B
	DB 01000000B	;RIGHT HAND BLACK BISHOP

	DB 0
	DB 0
	DB 0
	DB 01110001B
	DB 01010001B
	DB 01001111B
	DB 01111000B
	DB 00000100B	;LEFT HAND BLACK ROOK

	DB 0
	DB 0
	DB 0
 	DB 10001110B
	DB 10001010B
	DB 11110010B
	DB 00011110B
	DB 00100000B	;RIGHT HAND BLACK ROOK

	DB 0
	DB 10101010B
	DB 11111111B
	DB 10110000B
	DB 00001000B
	DB 00000100B
	DB 00000010B
	DB 00000100B	;LEFT HAND BLACK QUEEN

	DB 0
	DB 01010101B
	DB 11111111B
	DB 00001101B
	DB 00010000B
	DB 00100000B
	DB 01000000B
	DB 00100000B	;RIGHT HAND BLACK QUEEN

	DB 0
	DB 00000001B
	DB 00001111B
	DB 00000001B
	DB 00000001B
	DB 00011110B
	DB 00001000B
	DB 00000100B	;LEFT HAND BLACK KING

	DB 0
	DB 10000000B
	DB 11110000B
	DB 10000000B
	DB 10000000B
	DB 01111000B
	DB 00010000B
	DB 00100000B	;RIGHT HAND BLACK KING


	DB 00000010B
	DB 00000010B
	DB 00000100B
	DB 00000100B
	DB 00000100B
	DB 00001000B
	DB 00001111B
	DB 0		;LEFT HAND SMALL BLACK BASE

	DB 01000000B
	DB 01000000B
	DB 00100000B
	DB 00100000B
	DB 00100000B
	DB 00010000B
	DB 11110000B
	DB 0		;RIGHT HAND SMALL BLACK BASE


	DB 00000010B
	DB 00000100B
	DB 00001000B
	DB 00000100B
	DB 00000100B
	DB 00001000B
	DB 00010000B
	DB 00100000B	;LEFT HAND LARGE BLACK BASE

	DB 01000000B
	DB 00100000B
	DB 00010000B
	DB 00100000B
	DB 00100000B
	DB 00010000B
	DB 00001000B
	DB 00000100B	;RIGHT HAND LARGE BLACK BASE
;
;
;
;
;
;
;

;
;
INSPCE:	CALL DSPBRD
	RET
;
;
PGIFND:	LD HL,LINECT
	INC (HL)
	LD A,BOTLIN
	CP (HL)
	RET NC
	CALL DSPBRD
	CALL PRTTL
	LD A,1
	LD (LINECT),A
	RET
;
;
;
;
TBPLCL:	CALL PRMVNM
TBPLC2:	RET NZ
TBPLC1:	LD A,6
	JP SPACES
TBCPCL:	CALL PRMVNM
TBCPC2:	RET Z
	JR TBPLC1
TBPLMV:	CALL TAB1
	JR TBPLC2
TBCPMV:	CALL TAB1
	JR TBCPC2
PRMVNM:	LD DE,MVENUM
	LD A,3
	CALL PRTBLK
PRMV1:	LD A,(KOLOR)
	AND A
	RET
TAB1:	LD A,3
	CALL SPACES
	JR PRMV1
;

;
SUPMSG:	DEFM 'NEWBRAIN CHESS Version 2'
CPSMSG:	DEFM 'C.P. SOFTWARE (C) 1984   '
CWMSG:	DEFM 'Author: Chris Whittington'
ANAMSG:	DEFM 'Do you want to Play or Analyse ? '
CLRMSG:	DEFM 'Do you want to play White or Black ? '
FSTMES:	DEFM 'Fast or Slow mode ? '
TITLE1:	DEFM ' ME   '
TITLE2:	DEFM ' YOU '
OO:	DEFM ' O-O '
OOO:	DEFM 'O-O-O'
STMSG:	DEFM 'STALEMATE'
CKMSG:	DEFM 'CHECK'
MTMSG:	DEFM 'MATE IN '
PCS:	DEFM 'KQRBNP'
PCD:	DEFM 'INBRQK'
UWIN:	DEFM 'YOU WIN'
IWIN:	DEFM 'I WIN'
PLYDEP:	DEFM 'Select look ahead,   (0-6) '
WSMOVE:	DEFM 'Whose move is it ? '
PXPEP:	DEFM 'PXPEP'
INVAL1:	DEFM ' Illegal move   '
PCLMES:	DEFM 'Piece colour ? '
PCEMES:	DEFM 'Piece type ? '
SQMES:	DEFM 'Square to change ? '
MVDMES:	DEFM 'Has piece moved yet ? '
;
;
;
;
SUPLEN	EQU 24
CPSLEN	EQU 24
CWLEN	EQU 25
ANALEN	EQU 33
PCLLEN	EQU 14
PCELEN	EQU 13
SQLEN	EQU 19
WSMLEN	EQU 18
FSTLEN	EQU 20
MVDLEN	EQU 22
;

GENMOV:	CALL INCHK
	LD (CKFLG),A
	LD DE,(MLNXT)
	LD HL,(MLPTRI)
	INC HL
	INC HL
	LD (HL),E
	INC HL
	LD (HL),D
	INC HL
	LD (MLPTRI),HL
	LD (MLLST),HL
	LD A,21
GM5:	LD (M1),A
	LD HL,(M1)
	LD A,(HL)
	AND A
	JR Z,GM10
	CP -1
	JR Z,GM10
	LD (P1),A
	LD HL,COLOR
	XOR (HL)
	BIT 7,A
	CALL Z,MPIECE
GM10:	LD A,(M1)
	INC A
	CP 99
	JP NZ,GM5
	RET
INCHK:	LD A,(COLOR)
INCHK1:	LD HL,POSK
	AND A
	JR Z,$+3
	INC HL
	LD A,(HL)
	LD (M3),A
	LD HL,(M3)
	LD A,(HL)
	LD (P1),A
	AND 7
	LD (T1),A
	CALL ATTACK
	RET

ATTACK:	PUSH BC
	XOR A
	LD B,16
	LD (INDX2),A
	LD IY,(INDX2)
AT5:	LD C,(IY+DIRECT)
	LD D,0
	LD A,(M3)
	LD (M2),A
AT10:	INC D
	LD HL,M2
	LD A,(HL)
	ADD C
	LD (HL),A
	LD HL,(M2)
	LD A,(HL)
	CP -1
	JR Z,AT12
	LD (P2),A
	AND 7
	LD (T2),A
	JR NZ,AT12A
	LD A,B
	CP 9
	JR NC,AT10
AT12:	INC IY
	DJNZ AT5
	XOR A
AT13:	POP BC
	RET
AT12A:	LD A,(P2)
	LD HL,P1
	XOR (HL)
	JP P,AT14B
AT14A:	BIT 6,D
	JR NZ,AT12
	SET 5,D
	JP AT14
AT14B:	BIT 5,D
	JR NZ,AT12
	SET 6,D
AT14:	LD A,(T2)
	LD E,A
	LD A,B
	CP 9
	JR C,AT25
	LD A,E
	CP QUEEN
	JR NZ,AT15
	SET 7,D
	JR AT30
AT15:	LD A,D
	AND 0FH
	CP 1
	JR NZ,AT16
	LD A,E
	CP KING
	JR Z,AT30
AT16:	LD A,B
	CP 13
	JR C,AT21
	LD A,E
	CP BISHOP
	JR Z,AT30
	LD A,D
	AND 0FH
	CP 1
	JR NZ,AT12
	CP E
	JR NZ,AT12
	LD A,(P2)
	BIT 7,A
	JR Z,AT20
	LD A,B
	CP 15
	JR C,AT12
	JR AT30
AT20:	LD A,B
	CP 15
	JR NC,AT12
	JR AT30
AT21:	LD A,E
	CP ROOK
	JR NZ,AT12
	JR AT30
AT25:	LD A,E
	CP KNIGHT
	JR NZ,AT12
AT30:	LD A,(T1)
	CP 7
	JR Z,AT31
	BIT 5,D
	JR Z,AT32
	LD A,1
	JP AT13
AT31:	CALL ATKSAV
AT32:	LD A,(T2)
	CP KING
	JP Z,AT12
	CP KNIGHT
	JP Z,AT12
	JP AT10

MOVE:	LD HL,(MLPTRJ)
	INC HL
	INC HL
MV1:	LD A,(HL)
	LD (M1),A
	INC HL
	LD A,(HL)
	LD (M2),A
	INC HL
	LD D,(HL)
	LD IX,(M1)
	LD E,(IX+BOARD)
	BIT 5,D
	JR NZ,MV15
	LD A,E
	AND 7
	CP QUEEN
	JR Z,MV20
	CP KING
	JR Z,MV30
MV5:	LD IY,(M2)
	SET 3,E
	LD (IY+BOARD),E
	LD (IX+BOARD),0
	BIT 6,D
	JR NZ,MV40
	LD A,D
	AND 7
	CP QUEEN
	RET NZ
	LD HL,POSQ
	BIT 7,D
	JR Z,MV10
	INC HL
MV10:	XOR A
	LD (HL),A
	RET
MV15:	SET 2,E
	JP MV5
MV20:	LD HL,POSQ
MV21:	BIT 7,E
	JR Z,MV22
	INC HL
MV22:	LD A,(M2)
	LD (HL),A
	JP MV5
MV30:	LD HL,POSK
	BIT 6,D
	JR Z,MV21
	SET 4,E
	JP MV21
MV40:	LD HL,(MLPTRJ)
	LD DE,8
	ADD HL,DE
	JP MV1
UNMOVE:	LD HL,(MLPTRJ)
	INC HL
	INC HL
UM1:	LD A,(HL)
	LD (M1),A
	INC HL
	LD A,(HL)
	LD (M2),A
	INC HL
	LD D,(HL)
	LD IX,(M2)
	LD E,(IX+BOARD)
	BIT 5,D
	JR NZ,UM15
	LD A,E
	AND 7
	CP QUEEN
	JR Z,UM20
	CP KING
	JR Z,UM30
UM5:	BIT 4,D
	JR NZ,UM16
UM6:	LD IY,(M1)
	LD (IY+BOARD),E
	LD A,D
	AND 8FH
	LD (IX+BOARD),A
	BIT 6,D
	JR NZ,UM40
	LD A,D
	AND 7
	CP QUEEN
	RET NZ
	LD HL,POSQ
	BIT 7,D
	JR Z,UM10
	INC HL
UM10:	LD A,(M2)
	LD (HL),A
	RET
UM15:	RES 2,E
	JP UM5
UM16:	RES 3,E
	JP UM6
UM20:	LD HL,POSQ
UM21:	BIT 7,E
	JR Z,UM22
	INC HL
UM22:	LD A,(M1)
	LD (HL),A
	JP UM5
UM30:	LD HL,POSK
	BIT 6,D
	JR Z,UM21
	RES 4,E
	JP UM21
UM40:	LD HL,(MLPTRJ)
	LD DE,8
	ADD HL,DE
	JP UM1
MPIECE:	XOR (HL)
	AND 87H
	CP BPAWN
	JR NZ,$+3
	DEC A
	AND 7
	LD (T1),A
	LD IY,(T1)
	LD B,(IY+DCOUNT)
	LD A,(IY+DPOINT)
	LD (INDX2),A
	LD IY,(INDX2)
MP5:	LD C,(IY+DIRECT)
	LD A,(M1)
	LD (M2),A
MP10:	LD HL,M2
	LD A,(HL)
	ADD C
	LD (HL),A
	LD HL,(M2)
	LD A,(HL)
	CP -1
	JR Z,MP15
	LD (P2),A
	AND 7
	LD (T2),A
	JP Z,MP10A
	LD A,(P2)
	LD HL,P1
	XOR (HL)
	JP P,MP15
	LD A,1
MP10A:	AND A
	EX AF,AF
	LD A,(T1)
	CP PAWN+1
	JR C,MP20
	CALL ADMOVE
	EX AF,AF
	JR NZ,MP15
	LD A,(T1)
	CP KING
	JR Z,MP15
	CP BISHOP
	JR NC,MP10
MP15:	INC IY
	DJNZ MP5
	LD A,(T1)
	CP KING
	CALL Z,CASTLE
	RET
MP20:	LD A,B
	CP 3
	JR C,MP35
	JR Z,MP30
	EX AF,AF
	JR NZ,MP15
	LD A,(M2)
	CP 91
	JR NC,MP25
	CP 29
	JR NC,MP26
MP25:	LD HL,P2
	SET 5,(HL)
MP26:	CALL ADMOVE
	INC IY
	DEC B
	LD HL,P1
	BIT 3,(HL)
	JR Z,MP10
	JR MP15
MP30:	EX AF,AF
	JR NZ,MP15
MP31:	CALL ADMOVE
	JR MP15
MP35:	EX AF,AF
	JR Z,MP36
	LD A,(M2)
	CP 91
	JR NC,MP37
	CP 29
	JR NC,MP31
MP37:	LD HL,P2
	SET 5,(HL)
	JR MP31
MP36:	CALL ENPSNT
	JP MP15
ADMOVE:	LD DE,(MLNXT)
	LD HL,MLEND
	AND A
	SBC HL,DE
	JR C,AM10
	LD HL,(MLLST)
	LD (MLLST),DE
	LD (HL),E
	INC HL
	LD (HL),D
	LD HL,P1
	BIT 3,(HL)
	JR NZ,$+7
	LD HL,P2
	SET 4,(HL)
	EX DE,HL
	LD (HL),0
	INC HL
	LD (HL),0
	INC HL
	LD A,(M1)
	LD (HL),A
	INC HL
	LD A,(M2)
	LD (HL),A
	INC HL
	LD A,(P2)
	LD (HL),A
	INC HL
	LD (HL),0
	INC HL
	LD (MLNXT),HL
	RET
AM10:	LD (HL),0
	INC HL
	LD (HL),0
	DEC HL
	RET
ATKSAV:	PUSH BC
	PUSH DE
	LD A,(NPINS)
	AND A
	CALL NZ,PNCK
	LD IX,(T2)
	LD HL,ATKLST
	LD BC,0
	LD A,(P2)
	BIT 7,A
	JR Z,$+4
	LD C,7
	AND 7
	POP DE
	PUSH DE
	LD E,A
	BIT 7,D
	JR Z,$+4
	LD E,QUEEN
	ADD HL,BC
	INC (HL)
	LD D,0
	ADD HL,DE
	LD A,(HL)
	AND 0FH
	JR Z,AS20
	LD A,(HL)
	AND 0F0H
	JR Z,AS19
	INC HL
	JR AS20
AS19:	RLD
	LD A,(IX+PVALUE)
	RRD
	JR AS25
AS20:	LD A,(IX+PVALUE)
	RLD
AS25:	POP DE
	POP BC
	RET
;
PINFND:	XOR A
	LD (NPINS),A
	LD DE,POSK
PF1:	LD A,(DE)
	AND A
	JP Z,PF26
	CP -1
	RET Z
	LD (M3),A
	LD IX,(M3)
	LD A,(IX+BOARD)
	LD (P1),A
	LD B,8
	XOR A
	LD (INDX2),A
	LD IY,(INDX2)
PF2:	LD A,(M3)
	LD (M2),A
	XOR A
	LD (M4),A
	LD C,(IY+DIRECT)
PF5:	LD HL,M2
	LD A,(HL)
	ADD C
	LD (HL),A
	LD IX,(M2)
	LD A,(IX+BOARD)
	CP -1
	JP Z,PF25
	LD (P2),A
	AND 7
	LD (T2),A
	JR Z,PF5
	LD A,(P2)
	LD HL,P1
	XOR (HL)
	LD A,(M4)
	JP P,PF15
	AND A
	JP Z,PF25
	LD A,(T2)
	CP QUEEN
	JP Z,PF19
	LD L,A
	LD A,B
	CP 5
	JR C,PF10
	LD A,L
	CP BISHOP
	JP NZ,PF25
	JP PF20
PF10:	LD A,L
	CP ROOK
	JP NZ,PF25
	JP PF20
PF15:	AND A
	JP NZ,PF25
	LD A,(M2)
	LD (M4),A
	JP PF5
PF19:	LD A,(P1)
	AND 7
	CP QUEEN
	JR NZ,PF20
	PUSH BC
	PUSH DE
	PUSH IY
	XOR A
	LD B,14
	LD HL,ATKLST
	LD (HL),A
	INC HL
	DJNZ $-2
	LD A,7
	LD (T1),A
	CALL ATTACK
	LD HL,WACT
	LD DE,BACT
	LD A,(P1)
	JP P,PF19A
	EX DE,HL
PF19A:	LD A,(HL)
	EX DE,HL
	SUB (HL)
	DEC A
	POP IY
	POP DE
	POP BC
	JP PF25
PF20:	LD HL,NPINS
	INC (HL)
	LD IX,(NPINS)
	LD (IX+PLISTD),C
	LD A,(M4)
	LD (IX+PLIST),A
PF25:	INC IY
	DEC B
	JP NZ,PF2
PF26:	INC DE
	JP PF1

PNCK:	LD D,C
	LD E,0
	LD C,A
	LD B,0
	LD A,(M2)
	LD HL,PLISTA
PC1:	CPIR
	RET NZ
	EX AF,AF
	BIT 0,E
	JR NZ,PC5
	SET 0,E
	PUSH HL
	POP IX
	LD A,(IX+9)
	CP D
	JR Z,PC3
	NEG
	CP D
	JR NZ,PC5
PC3:	EX AF,AF
	JP PE,PC1
	RET
PC5:	POP AF
	POP DE
	POP BC
	RET
;
CASTLE:	LD A,(P1)
	BIT 3,A
	RET NZ
	LD A,(CKFLG)
	AND A
	RET NZ
	LD BC,0FF03H
CA5:	LD A,(M1)
	ADD C
	LD C,A
	LD (M3),A
	LD IX,(M3)
	LD A,(IX+BOARD)
	AND 7FH
	CP ROOK
	JR NZ,CA20
	LD A,C
	JR CA15
CA10:	LD IX,(M3)
	LD A,(IX+BOARD)
	AND A
	JR NZ,CA20
	LD A,(M3)
	CP 22
	JR Z,CA15
	CP 92
	JR Z,CA15
	CALL ATTACK
	AND A
	JR NZ,CA20
	LD A,(M3)
CA15:	ADD B
	LD (M3),A
	LD HL,M1
	CP (HL)
	JR NZ,CA10
	SUB B
	SUB B
	LD (M2),A
	LD HL,P2
	LD (HL),40H
	CALL ADMOVE
	LD HL,M1
	LD A,(HL)
	LD (HL),C
	SUB B
	LD (M2),A
	XOR A
	LD (P2),A
	CALL ADMOVE
	CALL ADJPTR
	LD A,(M3)
	LD (M1),A
CA20:	LD A,B
	CP 1
	RET Z
	LD BC,01FCH
	JP CA5
ENPSNT:	LD A,(M1)
	LD HL,P1
	BIT 7,(HL)
	JR Z,$+4
	ADD 10
	CP 61
	RET C
	CP 69
	RET NC
	LD IX,(MLPTRJ)
	BIT 4,(IX+MLFLG)
	RET Z
	LD A,(IX+MLTOP)
	LD (M4),A
	LD IX,(M4)
	LD A,(IX+BOARD)
	LD (P3),A
	AND 7
	CP PAWN
	RET NZ
	LD A,(M4)
	LD HL,M2
	SUB (HL)
	JP P,$+5
	NEG
	CP 10
	RET NZ
	LD HL,P2
	SET 6,(HL)
	CALL ADMOVE
	LD A,(M1)
	LD (M3),A
	LD A,(M4)
	LD (M1),A
	LD (M2),A
	LD A,(P3)
	LD (P2),A
	CALL ADMOVE
	LD A,(M3)
	LD (M1),A
ADJPTR:	LD HL,(MLLST)
	LD DE,-6
	ADD HL,DE
	LD (MLLST),HL
	LD (HL),0
	INC HL
	LD (HL),0
	RET

BOOK:	LD A,(BOKFL)
	CP 1
	RET NZ
	LD A,(MOVENO)
	CP 1
	JR NZ,BOK5
	LD A,0
	LD (BOKNO),A
	LD A,(KOLOR)
	AND A
	JR Z,BOK60
BOK5:	LD A,(BOKNO)
	CALL BOKCVT
	INC HL
	INC HL
	INC HL
	INC HL
BOK25:	LD A,(HL)
	CP 0
	JR Z,BOK100
	PUSH HL
	CALL BOKCVT
	INC HL
	LD IX,(MLPTRJ)
	LD A,(IX+MLFRP)
	CP (HL)
	JR NZ,BOK37
	LD A,(IX+MLTOP)
	INC HL
	CP (HL)
	JR Z,BOK40
BOK37:	POP HL
	INC HL
	JR BOK25
BOK40:	POP HL
	LD A,(HL)
	LD (BOKNO),A
BOK60:	LD A,(BOKNO)
	CALL BOKCVT
	INC HL
	INC HL
	INC HL
	INC HL
	LD A,(HL)
	CP 0
	JR Z,BOK100
BOK75:	LD (BOKNO),A
	CALL RND
	JR Z,BOK80
	INC HL
	LD A,(HL)
	CP 0
	JR NZ,BOK75
BOK80:	LD A,(BOKNO)
	CALL BOKCVT
	DEC HL
	LD (BESTM),HL
	LD HL,SCORE+1
	LD (HL),0
	POP AF
	RET
BOK100:	LD A,0
	LD (BOKFL),A
	RET
BOKCVT:	LD HL,BMOVE
	LD B,A
BOKC5:	LD A,B
	CP 0
	RET Z
BOKC10:	INC HL
	LD A,(HL)
	CP 0
	JR NZ,BOKC10
	INC HL
	DEC B
	JR BOKC5

BMOVE:	DB 0,-1,-1,-1,1,2,0
	DB 1,35,55,16,3,8,11,14,0	;E2E4
	DB 2,34,54,16,17,20,0	;D2D4
	DB 3,85,65,16,4,0		;E7E5
	DB 4,27,46,16,5,0		;G1F3
	DB 5,92,73,16,6,0		;B8C6
	DB 6,26,62,16,7,0		;F1B5
	DB 7,81,71,16,0		;A2A3
	DB 8,83,63,16,9,0		;C7C5
	DB 9,27,46,16,10,0		;G1F3
	DB 10,92,73,16,0		;B8C6
	DB 11,85,75,16,12,0	;E7E6
	DB 12,34,54,16,13,0	;D2D4
	DB 13,84,64,16,23,0	;D7D5
	DB 14,83,73,16,15,0	;C7C6
	DB 15,34,54,16,16,0	;D2D4
	DB 16,84,64,16,0		;D7D5
	DB 17,84,64,16,18,0	;D7D5
	DB 18,33,53,16,19,26,27,0	;C2C4
	DB 19,97,76,16,0		;G8F6
	DB 20,97,76,16,21,0	;G8F6
	DB 21,33,53,16,22,0	;C2C4
	DB 22,85,75,16,29,0	;E7E6
	DB 23,22,43,16,24,0	;B1C3
	DB 24,97,76,16,25,0	;G8F6
	DB 25,55,65,16,0		;E4E5
        DB 26,83,73,16,28,0	;C7C6
        DB 27,85,75,16,28,0	;E7E6
        DB 28,23,56,16,0		;F1C4
	DB 29,23,67,1,0		;B1G5


FNDMOV:	LD HL,STALEF
	SET 0,(HL)
	CALL BOOK
	CALL ENCODE
	LD HL,KCLIST
	LD (KCAPTR),HL
	LD B,20
	XOR A
FM1:	LD (HL),A
	INC HL
	DJNZ FM1
	LD HL,KLIST
	LD (KPTR),HL
	LD B,20
	XOR A
FM2:	LD (HL),A
	INC HL
	DJNZ FM2
	LD HL,ENDF		
	LD (HL),0		
	LD HL,NOQENF
	SET 0,(HL)
	EX DE,HL
	LD HL,CUMMTL		
	LD (HL),0		
	LD A,21
FM3:	LD (M3),A
	LD IX,(M3)
	LD A,(IX+BOARD)
	CP -1
	JR Z,FM4
	AND A
	JR Z,FM4
	AND 7
	CP QUEEN
	JR NZ,FM3A
	EX DE,HL		
	RES 0,(HL)		
	EX DE,HL		
FM3A:	ADD (HL)		
	LD (HL),A		
FM4:	LD A,(M3)
	INC A
	CP 99
	JR NZ,FM3
	LD HL,CUMMTL
	LD A,(HL)
	CP 47			
	JR NC,FM4H
	LD HL,ENDF
	SET 0,(HL)
	LD A,(NOQENF)
	AND A
	JR Z,FM4H		
	LD A,(VALPCE)
	CP 2
	JR Z,FM4A
	LD B,6
	LD HL,VALPCE
FM4D:	SLA (HL)		
	INC HL
	DJNZ FM4D
	JR FM4A
FM4H:	LD A,(VALPCE)
	CP 1
	JR Z,FM4A
	LD B,6
	LD HL,VALPCE
FM4HL:	SRA (HL)
	INC HL
	DJNZ FM4HL		
FM4A:	LD A,(NOQENF)
	AND A
	JR Z,FM4B
	LD A,(CUMMTL)
	CP 31			
	JR NC,FM4B		
	LD A,(QPLY)
;COND	EXTRA=1
	INC A
;ENDC
	JP FM4C
FM4B:	LD A,(QPLY)
FM4C:	LD (PLYMAX),A
	XOR A
	LD (STALEF),A
	LD (NPLY),A
	LD (BESTF),A
	LD (BLANKF),A
	LD (STOPF),A
	LD (CLOCK),A
	LD (CLOCK+1),A
	LD HL,0
	LD (BESTM),HL
	LD HL,MLIST
	LD (MLNXT),HL
	LD HL,PLYIX-2
	LD (MLPTRI),HL
	LD HL,SCORE
	LD (SCRIX),HL
	LD A,(PLYMAX)
	ADD 2
	LD B,A
	XOR A
	LD (HL),A
	INC HL
	DJNZ $-2
	LD (BC0),A
	LD (MV0),A
	LD (BP0),A
	CALL PINFND
	CALL POINTS
	LD A,(BRDC)
	LD (BC0),A
	LD A,(MTRL)
	LD (MV0),A
	LD A,(BRDP)
	LD (BP0),A
FM5:	LD HL,NPLY
	INC (HL)
	XOR A
	LD (MATEF),A
	LD HL,(KCAPTR)
	INC HL
	INC HL
	LD (KCAPTR),HL
	LD HL,(KPTR)
	INC HL
	INC HL
	LD (KPTR),HL
	CALL GENMOV
	LD HL,PLYMAX
	LD A,1
	CP (HL)
	JR Z,FM10
	LD A,(NPLY)
	CP 1
	JR NZ,FM6
	CALL SORTM
	JR FM10
FM6:	CALL SORTCK
	LD HL,PLYMAX
	LD A,(NPLY)
	CP (HL)
	JR Z,FM8
	JR NC,FM10		
FM8:	XOR A
	LD (PRUNEF),A
	LD (PRUNCT),A
FM9:	LD IX,(MLPTRJ)
	LD B,(IX+MLVAL)
	PUSH BC
	PUSH IX
	CALL PINFND
	CALL POINTX
	POP IX
	POP BC
	LD (IX+MLVAL),B
	LD HL,(SCRIX)
	SUB RANGE
	NEG
	INC HL
	CP (HL)
;
	JR NC,FM10
	LD HL,PRUNEF
	SET 0,(HL)
FM10:	LD HL,(MLPTRI)
	LD (MLPTRJ),HL
FM15:	RST 32
	DB 54
	JR NC,FM15S
	LD E,3
	LD A,12
	RST 32
	DB 48
	LD A,1
	LD (STOPF),A
FM15S:	LD A,(FAST)
	AND A
	JR Z,FM15A
	LD A,(BLANKF)
	AND A
	JR NZ,FM15A
	LD A,(CLOCK)
	CP 50
	JR C,FM15A
	LD A,1
	LD (BLANKF),A
	LD A,(STOPF)
	AND A
	JR NZ,FM15A
	CALL DISABL
FM15A:	LD A,(BESTF)
	AND A
	JR Z,FM15X
	LD A,(NPLY)
	CP 1
	JR NZ,FM15X
	LD A,(STOPF)
	AND A
	JP NZ,FM50
FM15X:	LD HL,(MLPTRJ)
	LD E,(HL)
	INC HL
	LD D,(HL)
	LD A,D
	AND A
	JP Z,FM25
	LD (MLPTRJ),DE
	LD HL,(MLPTRI)
	LD (HL),E
	INC HL
	LD (HL),D
;
	LD A,(NPLY)
	LD HL,PLYMAX
	CP (HL)
	JR C,FM18
	CALL MOVE
	CALL INCHK
	AND A
	JR Z,$+8
	CALL UNMOVE
	JP FM15
	LD HL,STALEF
	SET 0,(HL)
	LD A,(NPLY)
	LD HL,PLYMAX
	CP (HL)
	JR NZ,FM35
	LD A,(COLOR)
	XOR 80H
	CALL INCHK1
	AND A
	JR Z,FM35
	JP FM19
FM18:	CALL MOVE
	CALL INCHK
	AND A
	JR Z,FM19
	CALL UNMOVE
	JP FM15
FM19:	LD HL,STALEF
	SET 0,(HL)
	LD HL,COLOR
	LD A,80H
	XOR (HL)
	LD (HL),A
	BIT 7,A
	JR NZ,$+6
	LD HL,MOVENO
	INC (HL)
	LD HL,(SCRIX)
	LD A,(HL)
	INC HL
	INC HL
	LD (HL),A
	DEC HL
	LD (SCRIX),HL
	JP FM5
FM25:	LD A,(MATEF)
	AND A
	JR NZ,FM30
	LD A,(CKFLG)
	AND A
	LD A,80H
	JP Z,FM36
	LD A,(MOVENO)
	LD (PMATE),A
	LD A,(NPLY)
	CP 3
	LD A,0FFH
	JP C,FM36	
	LD A,0FEH
	JP FM36
FM30:	LD A,(NPLY)
	CP 1
	JP Z,FM50
	CALL ASCEND
	LD HL,(SCRIX)
	INC HL
	INC HL
	LD A,(HL)
	DEC HL
	DEC HL
	JP FM37
FM35:	LD HL,PLYMAX
	LD A,(NPLY)
	CP (HL)
	JR NZ,FM35X
	LD A,1
	CP (HL)
	JR Z,FM35X		
	LD A,(PRUNEF)
	AND A
	JR Z,FM35X
	LD IX,(MLPTRJ)
	LD A,(IX+MLVAL)
	CP 13			;KILLER OR CAPTURE
	JR NC,FM35X		;YES
	LD A,(IX+MLFLG)
	AND 7
	JR NZ,FM35X		;BAD CAPTURES TOO
	BIT 5,(IX+MLFLG)
	JR NZ,FM35X		;PAWN PROMOTION
	LD HL,ENDF
	AND A
	JR Z,FM35P
	LD A,(IX+MLFLG)
	AND 7
	CP PAWN
	JR Z,FM35X
FM35P:	LD A,(PRUNCT)
	INC A
	LD (PRUNCT),A
	LD B,PRUNNO+1		;PASS 2 MOVES MORE
	LD HL,PLYMAX
	BIT 0,(HL)
	JR NZ,FM35B		;AT ODD PLY
	LD B,PRUNNO+3		;2 MORE IF EVEN PLY
FM35B:	CP B
	JR C,FM35X
	CALL UNMOVE
	JP FM15
FM35X:	CALL PINFND
	CALL POINTS
	CALL UNMOVE
	LD A,(VALM)
FM36:	LD HL,MATEF
	SET 0,(HL)
	LD HL,(SCRIX)
;
FM37:	CP (HL)
	JR C,FM40
	JR Z,FM40
	NEG
	INC HL
	CP (HL)
	JP C,FM15
	JP Z,FM15
	LD (HL),A
	LD A,(NPLY)
	BIT 0,A
	CALL NZ,KILLER
	LD A,(NPLY)
	CP 1
	JP NZ,FM15
	LD HL,(MLPTRJ)
	LD (BESTM),HL
	LD A,1
	LD (BESTF),A
	LD A,(SCORE+1)
	CP 0FFH
	JP NZ,FM15
	LD HL,PLYMAX
	DEC (HL)
	DEC (HL)
	LD A,(KOLOR)
	BIT 7,A
	JP Z,FM50
	LD HL,PMATE
	DEC (HL)
	JP FM50
FM40:	LD A,(NPLY)
	BIT 0,A
	CALL Z,KILLER
	CALL ASCEND
	JP FM15
FM50:	CALL ENCODE
	CALL ENABLE
	RET
;
KILLER:	LD IX,(MLPTRJ)
	LD A,(IX+MLFLG)
	AND 7
	LD HL,(KCAPTR)
	JR NZ,KL10
	LD HL,(KPTR)
KL10:	LD A,(IX+MLFRP)
	LD (HL),A
	INC HL
	LD A,(IX+MLTOP)
	LD (HL),A
	RET
KPOINT:	LD IX,(MLPTRJ)
	LD A,(IX+MLFLG)
	AND 7
	JR Z,KP5
	LD HL,(KCAPTR)
	LD A,(IX+MLFRP)
	CP (HL)
	JR NZ,KP5
	LD A,(IX+MLTOP)
	INC HL
	CP (HL)
	JR NZ,KP5
	LD B,16			;KILLER CAPTURE BONUS
	JP KP500
KP5:	LD HL,BOARDA
	LD L,(IX+MLFRP)
	LD A,(HL)
	AND 7
	CP BISHOP
	LD A,(HL)
	JR NZ,KP6
	DEC A			;BISHOP TO KNIGHT
KP6:	LD (P1),A
	BIT 7,A
	JP NZ,KP50		;BLACK'S MOVE
	LD A,(IX+MLFLG)
	AND 7
	JR Z,KP20		;NO PIECE IN (TO)
	ADD A
	ADD A
	ADD A
	ADD A
	LD B,A			;SAVE PIECE TIMES 16
	LD HL,SRTATN
	LD L,(IX+MLTOP)
	LD A,(HL)
	AND A			;(TO) UNDER WHITE CONTROL?
	JR Z,KP10		;NO ONE CONTROLS
	JP P,KP500		;YES, SCORE IN B REG
KP10:	LD A,(IX+MLFLG)
	AND 7			;GET (TO) PIECE
	CP BISHOP
	JR NZ,KP11
	DEC A
KP11:	LD C,A
	LD A,(P1)
	AND 7
	CP C			;(FROM)-(TO)
	JP C,KP500		;BONUS FOR GOOD CAPTURE
	JP Z,KP400		;BONUS,EQUAL CAPTURE
KP20:	LD HL,SRTATN
	LD L,(IX+MLTOP)
	LD A,(HL)
	BIT 7,A			;IS (TO) BLACK CONTROLLED?
	JP NZ,KP600		;YES, BAD SCORE
	AND A
	JR NZ,KP25		;WHITE CONTROLS
	LD DE,MINATB
	LD E,L
	EX DE,HL
	LD A,(HL)
	AND A
	JR Z,KP25		;NO ATTACKERS
	LD A,(P1)
	AND 7			;ONLY PAWNS
	CP PAWN			;ARE SAFE
	JP NZ,KP600
KP25:	LD DE,MINATB		;ATTACKER < MOVING PIECE?
	LD E,L
	EX DE,HL
	XOR A
	CP (HL)
	JR Z,KP30
	LD A,(P1)
	AND 7
	CP (HL)			;(FROM)-ATTACKER
	JR Z,KP30
	JP NC,KP600		;YES, BAD SCORE
KP30:	LD HL,MINATB
	LD L,(IX+MLFRP)
	XOR A
	CP (HL)
	JP Z,KP200
	LD A,(P1)
	AND 7
	CP (HL)			;(FROM)-ATTACKER
	JP Z,KP200		;SAFE
	JP C,KP200		;SAFE
	LD B,15
	JP KP500
KP50:	LD A,(IX+MLFLG)		;HERE FOR BLACK MOVE
	AND 7
	JR Z,KP120		;NO PIECE IN (TO)
	ADD A
	ADD A
	ADD A
	ADD A
	LD B,A			;SAVE PIECE TIMES 16
	LD HL,SRTATN
	LD L,(IX+MLTOP)
	LD A,(HL)
	AND A			;(TO) UNDER BLACK CONTROL?
	JR Z,KP110		;NO ONE CONTROLS
	JP M,KP500		;YES, SCORE IN B REG
KP110:	LD A,(IX+MLFLG)
	AND 7			;GET (TO) PIECE
	CP BISHOP
	JR NZ,KP111
	DEC A
KP111:	LD C,A
	LD A,(P1)
	AND 7
	CP C			;(FROM)-(TO)
	JP C,KP500		;BONUS FOR GOOD CAPTURE
	JP Z,KP400		;BONUS,EQUAL CAPTURE
KP120:	LD HL,SRTATN
	LD L,(IX+MLTOP)
	LD A,(HL)
	AND A			;IS (TO) WHITE CONTROLLED?
	JP M,KP125		;NO
	JP NZ,KP600		;WHITE CONTROLS
	LD DE,MINATW
	LD E,L
	EX DE,HL
	LD A,(HL)
	AND A
	JR Z,KP125			;NO ATTACKERS
	LD A,(P1)
	AND 7			;ONLY PAWNS
	CP PAWN			;CAN MOVE IN
	JP NZ,KP600		;SAFELY
KP125:	LD DE,MINATW		;ATTACKER < MOVING PIECE?
	LD E,L
	EX DE,HL
	XOR A
	CP (HL)
	JR Z,KP130
	LD A,(P1)
	AND 7
	CP (HL)			;(FROM)-ATTACKER
	JR Z,KP130
	JP NC,KP600		;YES, BAD SCORE
KP130:	LD HL,MINATW
	LD L,(IX+MLFRP)
	XOR A
	CP (HL)
	JR Z,KP200
	LD A,(P1)
	AND 7
	CP (HL)			;(FROM)-ATTACKER
	JR Z,KP200		;SAFE
	JR C,KP200		;SAFE
	LD B,15
	JP KP500
KP200:	LD HL,(KPTR)		;LOOK FOR KILLER MOVE
	LD A,(IX+MLFRP)
	CP (HL)
	JR NZ,KP220
	INC HL
	LD A,(IX+MLTOP)
	CP (HL)
	JR NZ,KP220
	LD B,14			;BONUS FOR KILLER MOVE
	JP KP500
KP220:	LD A,(ENDF)
	AND A
	JR Z,KP240		;NOT ENDING
	LD HL,BOARDA
	LD L,(IX+MLFRP)
	LD A,(HL)
	AND 7			;PIECE TYPE
	CP KING
	JR NZ,KP230
	LD HL,KEND
	JR KP250
KP230:	CP PAWN
	JR NZ,KP240
	LD A,(HL)
	LD HL,BPEND
	BIT 7,A
	JR NZ,KP250		;SKIP IF BLACK
	LD HL,WPEND
	JR KP250
KP240:	LD HL,BOARDA		;SET HL TO MATRIX
	LD L,(IX+MLFRP)
	LD A,(HL)		;FROM PIECE
	LD HL,BVLMAT
	BIT 7,A
	JR NZ,KP242		;SKIP IF BLACK
	LD HL,WVLMAT
KP242:	AND 7			;PIECE TYPE
	DEC A
	ADD H
	LD H,A			;TYPE MATRIX
KP250:	LD L,(IX+MLTOP)
	LD A,(HL)
	LD L,(IX+MLFRP)
	SUB (HL)		;POS. WORTH OF MOVE
	ADD 6			;RANGE 1 TO 11
	LD B,A
	JP KP500
KP400:	LD A,B
	DEC A
	LD (VALM),A
	LD (IX+MLVAL),A
	RET
KP500:	LD A,B			;PUTS MOVE AT END OF LIST
	LD (VALM),A
	LD (IX+MLVAL),A
	RET
KP600:	XOR A			;BONUS IN (B)
	LD (VALM),A
	LD (IX+MLVAL),A
	RET
SORTCK:	CALL SRTMAT
	LD BC,(MLPTRI)
	LD DE,0
SK5:	LD H,B
	LD L,C
	LD C,(HL)
	INC HL
	LD B,(HL)
	LD (HL),D
	DEC HL
	LD (HL),E
	XOR A
	CP B
	RET Z
	LD (MLPTRJ),BC
	CALL KPOINT
	LD HL,(MLPTRI)
	LD BC,(MLPTRJ)
SK15:	LD E,(HL)
	INC HL
	LD D,(HL)
	XOR A
	CP D
	JR Z,SK25
	PUSH DE
	POP IX
	LD A,(VALM)
	CP (IX+MLVAL)
	JR C,SK30
SK25:	LD (HL),B
	DEC HL
	LD (HL),C
	JP SK5
SK30:	EX DE,HL
	JP SK15
SRTMAT:	XOR A
	LD B,120
	LD DE,SRTATN
	LD HL,MINATW
ATM1:	LD (DE),A
	LD (HL),A
	INC H
	LD (HL),A
	DEC H
	INC DE
	INC HL
	DJNZ ATM1
	LD A,21
ATM5:	LD (M1),A
	LD HL,(M1)
	LD A,(HL)
	AND A
	JR Z,ATM10
	CP -1
	JR Z,ATM10
	LD (P1),A
	CALL SMOVIT
ATM10:	LD A,(M1)
	INC A
	CP 99
	JR NZ,ATM5
	RET
SMOVIT:	AND 87H
	CP BPAWN
	JR NZ,$+3
	DEC A
	AND 7
	LD (T1),A
	LD IY,(T1)
	LD B,(IY+DCOUNT)
	LD A,(IY+DPOINT)
	LD (INDX2),A
	LD IY,(INDX2)
MOV5:	LD C,(IY+DIRECT)
	LD A,(M1)
	LD (M2),A
MOV10:	LD HL,M2
	LD A,(HL)
	ADD C
	LD (HL),A
	LD HL,(M2)
	LD A,(HL)
	CP -1
	JR Z,MOV15
	AND 7
	LD (T2),A
	JR Z,MOV10A
	LD A,(P1)
	XOR (HL)		;(HL)=P2
	JP P,MOV36
	LD A,1
MOV10A:	AND A
	EX AF,AF
	LD A,(T1)
	CP PAWN+1
	JR C,MOV20
	CALL SADDIT
	EX AF,AF
	JR NZ,MOV15
	LD A,(T1)
	CP KING
	JR Z,MOV15
	CP BISHOP
	JP NC,MOV10
MOV15:	INC IY
	DJNZ MOV5
	RET
MOV20:	LD A,B
	CP 3
	JR C,MOV35
	EX AF,AF
	JR MOV15
MOV35:	EX AF,AF
	CALL SADDIT
	JR MOV15
MOV36:	LD A,1
	AND A
	EX AF,AF
	LD A,(T1)
	CP PAWN+1
	JR C,MOV20
	CALL SADDIT
	EX AF,AF
MOV40:	LD A,(T2)
	CP BISHOP
	JR Z,MOV40B
	CP ROOK
	JR Z,MOV40R
	CP QUEEN
	JR Z,MOV40Q
	JR MOV15
MOV40B:	LD A,(T1)
	CP QUEEN
	JR NZ,MOV15
	LD A,B
	CP 5
	JP NC,MOV10
	JR MOV15
MOV40R:	LD A,(T1)
	CP QUEEN
	JR NZ,MOV41
	LD A,B
	CP 5
	JP C,MOV10
	JR MOV15
MOV40Q:	LD A,(T1)
	CP BISHOP
	JP Z,MOV10
MOV41:	CP ROOK
	JP Z,MOV10
	JR MOV15
SADDIT:	PUSH BC
	LD HL,SRTATN
	LD DE,MINATW
	LD BC,MINATB
	LD A,(M2)
	LD L,A
	LD E,A
	LD C,A
	LD A,(P1)
	BIT 7,A
	JR NZ,SAD20
	INC (HL)
	AND 7
	CP 3
	JR NZ,SAD5
	LD A,2			;BISHOP TO KNIGHT
SAD5:	LD H,A
	LD A,(DE)
	AND A
	JR Z,SAD10
	CP H
	JR C,SAD50
SAD10:	LD A,H
	LD (DE),A
	JR SAD50
SAD20:	DEC (HL)
	AND 7
	CP 3
	JR NZ,SAD25
	LD A,2			;BISHOP TO KNIGHT
SAD25:	LD H,A
	LD A,(BC)
	AND A
	JR Z,SAD30
	CP H
	JR C,SAD50
SAD30:	LD A,H
	LD (BC),A
SAD50:	POP BC
	RET
;
POINTS:	CALL SRTMAT
POINTX:	LD HL,CLOCK
	INC (HL)
	XOR A
	LD (MTRL),A
	LD (BRDC),A
	LD (BRDP),A
	LD (PTSL),A
	LD (PTSW1),A
	LD (PTSW2),A
	LD (PTSCK),A
	LD HL,T1
	LD (HL),7
	LD A,21
PT5:	LD (M3),A
	LD IX,(M3)
	LD A,(IX+BOARD)
	CP -1
	JP Z,PT25
	LD HL,P1
	LD (HL),A
	AND 7
	LD (T3),A
	CP KNIGHT
	JR C,PT6X
	CP ROOK
	JR C,PT6B
	CP KING
	JR Z,PT6AA
	CP QUEEN
	JR NZ,PT6QX
	LD A,(MOVENO)
	CP 10
	JR NC,PT6QX
	BIT 3,(HL)
	JR Z,PT6QX
	LD A,-12
	BIT 7,(HL)
	JR Z,$+4
	LD A,12
	JP PT6D
PT6QX:	LD A,(MOVENO)
	CP 8
	JR C,PT6A
	JP PT6X
PT6AA:	BIT 4,(HL)
	JR Z,PT6A
	LD A,9
	BIT 7,(HL)
	JR Z,PT6D
	LD A,-9
	JP PT6D
PT6A:	BIT 3,(HL)
	JR Z,PT6X
	JP PT6C
PT6B:	BIT 3,(HL)
	JR NZ,PT6X
PT6C:	LD A,-4
	BIT 7,(HL)
	JR Z,$+4
	LD A,4
PT6D:	LD HL,BRDC
	ADD (HL)
	LD (HL),A
PT6X:	LD HL,SRTATN
	LD DE,SCNTRE
	LD A,(M3)
	LD L,A
	LD E,A
	LD A,(DE)
	AND A
	LD B,A
	LD A,(HL)
	JR Z,PT7A
PT7:	LD HL,ENDF
	BIT 0,(HL)
	JR NZ,PT7A
	ADD A			;DOUBLE FOR
	DJNZ PT7		;CENTRE SQUARES
PT7A:	LD HL,BRDC
	ADD (HL)
	LD (HL),A
	LD A,(P1)
	AND A
	JP Z,PT25
	BIT 7,A
	LD HL,MINATW
	JR NZ,PT8
	LD HL,MINATB
PT8:	LD L,E			;HL..MIN ATTACKER
	LD A,(HL)
	AND A
	JR NZ,PT10		;ATTACKERS PRESENT
	LD A,(P1)
	AND 7
	LD (T3),A
	LD IX,(T3)
	LD D,(IX+PVALUE)
	SLA D
	JP PT12
PT10:	XOR A
	LD B,14
	LD HL,ATKLST
	LD (HL),A
	INC HL
	DJNZ $-2
	CALL ATTACK
	CALL XCHNG
	XOR A
	CP E
	JR Z,PT12
	DEC D
	LD A,(P1)
	LD HL,COLOR
	XOR (HL)
	BIT 7,A
	LD A,E
	JR NZ,PT20
	LD HL,PTSL
	CP (HL)
	JP C,PT23
	LD (HL),E
	LD IX,(MLPTRJ)
	LD A,(M3)
	CP (IX+MLTOP)
	JR NZ,PT23
	LD (PTSCK),A
	JR PT23
PT12:	LD IX,(M3)
	LD A,(ENDF)
	AND A
	JR Z,PT14		;NOT ENDING
	LD A,(IX+BOARD)		;GET PIECE
	AND 7
	CP KING
	JR NZ,PT13
	LD HL,KEND
	JR PT15X
PT13:	CP PAWN
	JR NZ,PT14
	LD A,(IX+BOARD)
	LD HL,BPEND
	BIT 7,A
	JR NZ,PT13X		;SKIP IF BLACK
	LD HL,WPEND
PT13X:	LD A,(M3)
	ADD L
	LD L,A
	LD A,(HL)
	ADD A			;DOUBLE FOR PAWNS IN END
	BIT 7,(IX+BOARD)
	JR Z,PT16
	NEG
	JP PT16
PT14:	LD A,(IX+BOARD)
	LD HL,BVLMAT
	BIT 7,A
	JR NZ,PT15
	LD HL,WVLMAT
PT15:	AND 7
	DEC A
	ADD H
	LD H,A
PT15X:	LD A,(M3)
	ADD L
	LD L,A			;HL POINTS TO SQ. VALUE
	LD A,(HL)
	BIT 7,(IX+BOARD)
	JR Z,PT16
	NEG			;IF BLACK
PT16:	LD HL,BRDP
	ADD (HL)
	LD (HL),A
	JR PT23
PT20:	LD HL,PTSW1
	CP (HL)
	JR C,$+4
	LD A,(HL)
	LD (HL),E
	LD HL,PTSW2
	CP (HL)
	JR C,PT23
	LD (HL),A
PT23:	LD HL,P1
	BIT 7,(HL)
	LD A,D
	JR Z,$+4
	NEG
	LD HL,MTRL
	ADD (HL)
	LD (HL),A
PT25:	LD A,(M3)
	INC A
	CP 99
	JP NZ,PT5
	LD A,(PTSCK)
	AND A
	JR Z,PT25A
	LD A,(PTSW2)
	LD (PTSW1),A
	XOR A
	LD (PTSW2),A
PT25A:	LD A,(PTSL)
	AND A
	JR Z,$+3
	DEC A
	LD B,A
	LD A,(PTSW1)
	AND A
	JR Z,$+11
	LD A,(PTSW2)
	AND A
	JR Z,$+5
	DEC A
	SRL A
	SUB B
	LD HL,COLOR
	BIT 7,(HL)
	JR Z,$+4
	NEG
	LD HL,MTRL
	ADD (HL)
	LD HL,MV0
	SUB (HL)
	LD B,A
	LD A,MATLIM
	CALL LIMIT
	LD E,A
	LD A,(BRDC)
	LD HL,BC0
	SUB (HL)
	LD B,A
	LD A,(BRDP)
	LD HL,BP0
	SUB (HL)
	ADD B
	LD B,A
	LD A,(PTSCK)
	AND A
	JR Z,$+4
	LD B,0
	LD A,POSLIM
	CALL LIMIT
	LD D,A
	LD A,E
	ADD A
	ADD A	
	ADD A
	ADD D
	LD HL,COLOR
	BIT 7,(HL)
	JR NZ,$+4
	NEG
	ADD 80H
	LD (VALM),A
	LD IX,(MLPTRJ)
	LD (IX+MLVAL),A
	RET
;
LIMIT:	BIT 7,B
	JR Z,LIM10
	NEG
	CP B
	RET NC
	LD A,B
	RET
LIM10:	CP B
	RET C
	LD A,B
	RET
ASCEND:	LD HL,COLOR
	LD A,80H
	XOR (HL)
	LD (HL),A
	BIT 7,A
	JR Z,$+6
	LD HL,MOVENO
	DEC (HL)
	LD HL,(SCRIX)
	DEC HL
	LD (SCRIX),HL
	LD HL,NPLY
	DEC (HL)
	LD HL,(MLPTRI)
	DEC HL
	LD D,(HL)
	DEC HL
	LD E,(HL)
	LD (MLNXT),DE
	DEC HL
	LD D,(HL)
	DEC HL
	LD E,(HL)
	LD (MLPTRI),HL
	LD (MLPTRJ),DE
	LD HL,(KCAPTR)
	DEC HL
	DEC HL
	LD (KCAPTR),HL
	LD HL,(KPTR)
	DEC HL
	DEC HL
	LD (KPTR),HL
	CALL UNMOVE
	RET
SORTM:	LD BC,(MLPTRI)
	LD DE,0
SR5:	LD H,B
	LD L,C
	LD C,(HL)
	INC HL
	LD B,(HL)
	LD (HL),D
	DEC HL
	LD (HL),E
	XOR A
	CP B
	RET Z
SR10:	LD (MLPTRJ),BC
	CALL EVAL
	LD HL,(MLPTRI)
	LD BC,(MLPTRJ)
SR15:	LD E,(HL)
	INC HL
	LD D,(HL)
	XOR A
	CP D
	JR Z,SR25
	PUSH DE
	POP IX
	LD A,(VALM)
	CP (IX+MLVAL)
	JR NC,SR30
SR25:	LD (HL),B
	DEC HL
	LD (HL),C
	JP SR5
SR30:	EX DE,HL
	JR SR15
EVAL:	CALL MOVE
	CALL PINFND
	CALL POINTS
	CALL UNMOVE
	RET
XCHNG:	EXX
	LD A,(P1)
	LD HL,WACT
	LD DE,BACT
	BIT 7,A
	JR Z,$+3
	EX DE,HL
	LD B,(HL)
	EX DE,HL
	LD C,(HL)
	EX DE,HL
	EXX
	LD C,0
	LD E,0
	LD IX,(T3)
	LD D,(IX+PVALUE)
	SLA D
	LD B,D
	CALL NEXTAD
	JR Z,XC30
XC10:	LD L,A
	CALL NEXTAD
	JR Z,XC18
	EX AF,AF
	LD A,B
	CP L
	JR NC,XC19
	EX AF,AF
XC15:	CP L
	JR C,XC30
	CALL NEXTAD
	JR Z,XC30
	LD L,A
	CALL NEXTAD
	JR NZ,XC15
XC18:	EX AF,AF
	LD A,B
XC19:	BIT 0,C
	JR Z,$+4
	NEG
	ADD E
	LD E,A
	EX AF,AF
	JR Z,XC30
	LD B,L
	JP XC10
XC30:	RET
NEXTAD:	INC C
	EXX
	LD A,B
	LD B,C
	LD C,A
	EX DE,HL
	XOR A
	CP B
	JR Z,NX6
	DEC B
	INC HL
	CP (HL)
	JR Z,$-2
	RRD
	ADD A
	DEC HL
NX6:	EXX
	RET
;
ENDPRG:	DB 0
;
	ORG 256*($/256+1)
SCNTRE:	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,1,1,1,1,0,0,0
	DB 0,0,0,1,2,2,1,0,0,0
	DB 0,0,0,1,2,2,1,0,0,0
	DB 0,0,0,1,1,1,1,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
INTERR:	LD DE,CLRMSG
	LD A,36
	CALL PRTBLK
	CALL CHARTR
	CALL CARRET
	CP 'W'
	LD A,80H
	JR Z,IN04
	SUB A
IN04:	LD (KOLOR),A
IN08:	LD DE,PLYDEP
	LD A,25
	CALL PRTBLK
	CALL CHARTR
	CALL CARRET
	LD HL,QPLY
	CP '0'
	JP M,IN08
	CP '7'
	JP P,IN08
	SUB '0'
	LD (HL),A
	INC (HL)
	LD DE,FSTMES
	LD A,FSTLEN
	CALL PRTBLK
	CALL CHARTR
	CP 'F'
	LD A,1
	JR Z,IN20
	XOR A
IN20:	LD (FAST),A
	RET

;
PLYRMV:	CALL LETVAL
	LD H,A
	CALL NUMVAL
	LD L,A
	CALL ASNTBI
	LD (MVEMSG),A
	LD A,'-'
	CALL PRTCHR
	CALL LETVAL
	LD H,A
	CALL NUMVAL
	LD L,A
	CALL ASNTBI
	LD (MVEMSG+1),A
	CALL VALMOV
	AND A
	JR NZ,PL08
	CALL EXECMV
	LD A,B
	AND A
	JR Z,PL1C
	CALL BKSP
	CALL BKSP
	CALL BKSP
	CALL BKSP
	CALL BKSP
	BIT 1,B
	JR Z,PL11
	LD DE,OO
	LD A,5
	CALL PRTBLK
	JR PL1C
PL11:	BIT 2,B
	JR Z,PL12
	LD DE,OOO
	LD A,5
	CALL PRTBLK
	JP PL1C
PL12:	LD DE,PXPEP
	LD A,5
	CALL PRTBLK
PL1C:	LD A,(KOLOR)
	LD (COLOR),A
	CALL ENCODE
	CALL INCHK
	PUSH AF
	CALL ENCODE
	POP AF
	AND A
	RET Z
	CALL CARRET
	CALL TBPLMV
	LD DE,CKMSG
	LD A,5
	CALL PRTBLK
	LD HL,LINECT
	INC (HL)			
	RET
PL08:	LD HL,LINECT
	INC (HL)
	CALL PGIFND
	LD DE,INVAL1
	LD A,14
	CALL PRTLIN
	CALL TBPLCL
	JP PLYRMV
;
	ORG 256*($/256+1)
BVLMAT:	DB 0,0,0,0,0,0,0,0,0,0	;BLACK PAWN
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,2,2,3,4,4,3,2,2,0
	DB 0,1,1,2,3,3,2,1,1,0
	DB 0,0,0,1,2,2,1,0,0,0
	DB 0,0,0,0,1,1,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;


;
	ORG BVLMAT+100H
	DB 0,0,0,0,0,0,0,0,0,0	;BLACK KNIGHT
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,3,0,0,3,0,0,0
	DB 0,0,2,4,4,4,4,2,0,0
	DB 0,0,0,3,3,3,3,0,0,0
	DB 0,0,0,0,2,2,0,0,0,0
	DB 0,0,0,1,0,0,1,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
;
FCDMAT:	LD A,(MOVENO)
	LD B,A
	LD A,(PMATE)
	SUB B
	AND A
	JR NZ,FCM0C
	BIT 0,C
	JR Z,FCM04
	CALL CARRET
	LD DE,MTMSG
	LD A,4
	CALL PRTLIN
	LD DE,UWIN
	LD A,7
	CALL PRTLIN
	JR FCM08
FCM04:	LD DE,MTMSG
	LD A,4
	CALL PRTLIN
	LD DE,IWIN
	LD A,5
	CALL PRTLIN
FCM08:	POP HL
	POP HL
	CALL CHARTR
FCM09:	CALL CLRSCR
	JP DRIV01
FCM0C:	BIT 0,C
	RET NZ
	CALL CARRET
	ADD 30H
	PUSH AF
	LD DE,MTMSG
	LD A,8
	CALL PRTBLK
	POP AF
	CALL PRTCHR
	CALL CARRET
	CALL TBPLMV
	RET


;
	ORG BVLMAT+200H
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,1,1,1,1,1,1,0,0	;BLACK BISHOP
	DB 0,0,1,2,2,2,2,1,0,0
	DB 0,0,0,1,2,2,1,0,0,0
	DB 0,0,0,1,2,2,1,0,0,0
	DB 0,0,0,1,0,0,1,0,0,0
	DB 0,0,1,0,0,0,0,1,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
BITASN:	SUB A
	LD E,10
	CALL DIVIDE
	DEC D
	ADD 60H
	LD L,A
	LD A,D
	ADD 30H
	LD H,A
	RET

ASNTBI:	LD A,L
	SUB 30H
	INC A
	LD D,A
	LD E,10
	CALL MLTPLY
	LD A,H
	SUB 40H
	ADD D
	RET


;
	ORG BVLMAT+300H
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,2,2,2,2,2,2,2,2,0	;BLACK ROOK
	DB 0,5,5,5,5,5,5,5,5,0
	DB 0,0,0,3,3,3,3,0,0,0
	DB 0,0,0,2,2,2,2,0,0,0
	DB 0,0,0,2,2,2,2,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,2,2,2,2,0,0,0
	DB 0,0,0,1,1,1,1,0,0,0
;
VALMOV:	LD HL,(MLPTRJ)
	PUSH HL
	LD A,(KOLOR)
	XOR 80H
	LD (COLOR),A
	LD HL,PLYIX-2
	LD (MLPTRI),HL
	LD HL,MLIST+1024
	LD (MLNXT),HL
;
	CALL ENCODE
	;
	CALL GENMOV
	LD IX,MLIST+1024
VA5:	LD A,(MVEMSG)
	CP (IX+MLFRP)
	JR NZ,VA6
	LD A,(MVEMSG+1)
	CP (IX+MLTOP)
	JR Z,VA7
VA6:	LD E,(IX+MLPTR)
	LD D,(IX+MLPTR+1)
	XOR A
	CP D
	JR Z,VA10
	PUSH DE
	POP IX
	JR VA5
VA7:	LD (MLPTRJ),IX
	CALL MOVE
	CALL INCHK
	AND A
	JR NZ,VA9
VA8:	POP HL
	JP VA12
VA9:	CALL UNMOVE
VA10:	LD A,1
	POP HL
	LD (MLPTRJ),HL
;
VA12:	PUSH HL
	PUSH AF
	CALL ENCODE
	POP AF
	POP HL
	RET



;
	ORG BVLMAT+400H
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0	;BLACK QUEEN
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
DIVIDE:	PUSH BC
	LD B,8
DD04:	SLA D
	RLA
	SUB E
	JP M,$+6
	INC D
	JR $+3
	ADD E
	DJNZ DD04
	POP BC
	RET
MLTPLY:	PUSH BC
	SUB A
	LD B,8
ML04:	BIT 0,D
	JR Z,$+3
	ADD E
	SRA A
	RR D
	DJNZ ML04
	POP BC
	RET



;
	ORG BVLMAT+500H
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0	;BLACK KING
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
EXECMV:	PUSH IX
	PUSH AF
	LD IX,(MLPTRJ)
	LD C,(IX+MLFRP)
	LD E,(IX+MLTOP)
	LD D,(IX+MLFLG)
	LD B,0
	BIT 6,D
	JR Z,EX10
	LD DE,6
	ADD IX,DE
	LD C,(IX+MLFRP)
	LD E,(IX+MLTOP)
	LD A,E
	CP C
	JR NZ,EX04
	INC B
	JR EX10
EX04:	CP 1AH
	JR NZ,EX08
	SET 1,B
	JR EX10
EX08:	CP 60H
	JR NZ,EX0C
	SET 1,B
	JR EX10
EX0C:	SET 2,B
EX10:	CALL MAKEMV
EX14:	POP AF
	POP IX
	RET


;
;
	ORG BVLMAT+600H
WVLMAT:	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0	;WHITE PAWN
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,1,1,0,0,0,0
	DB 0,0,0,1,2,2,1,0,0,0
	DB 0,1,1,2,3,3,2,1,1,0
	DB 0,2,2,3,4,4,3,2,2,0
	DB 0,0,0,0,0,0,0,0,0,0
;
;
MAKEMV:	PUSH AF
	PUSH BC
	PUSH DE
	PUSH HL
	CALL DSPBRD
	POP HL
	POP DE
	POP BC
	POP AF
	RET
;
;
CLBRD:	XOR A
	LD B,8
	LD IX,BOARDA
CL2:	LD (IX+21),A
	LD (IX+31),A
	LD (IX+41),A
	LD (IX+51),A
	LD (IX+61),A
	LD (IX+71),A
	LD (IX+81),A
	LD (IX+91),A
	INC IX
	DJNZ CL2
	LD IX,BOARDA
	LD (IX+25),6
	LD (IX+95),86H
	CALL ROYALT
	RET
;



;
	ORG WVLMAT+100H
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0	;WHITE KNIGHT
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,1,0,0,1,0,0,0
	DB 0,0,0,0,2,2,0,0,0,0
	DB 0,0,0,3,3,3,3,0,0,0
	DB 0,0,2,4,4,4,4,2,0,0
	DB 0,0,0,3,0,0,3,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
ROYALT:	LD HL,POSK
	LD B,4
	LD (HL),0
	INC HL
	DJNZ $-3
	LD A,21
RY04:	LD (M1),A
	LD HL,POSK
	LD IX,(M1)
	LD A,(IX+BOARD)
	BIT 7,A
	JR Z,$+3
	INC HL
	AND 7
	CP KING
	JR Z,RY08
	CP QUEEN
	JR NZ,RY0C
	INC HL
	INC HL
RY08:	LD A,(M1)
	LD (HL),A
RY0C:	LD A,(M1)
	INC A
	CP 99
	JR NZ,RY04
	RET



;
	ORG WVLMAT+200H
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0	;WHITE BISHOP
	DB 0,0,1,0,0,0,0,1,0,0
	DB 0,0,0,1,0,0,1,0,0,0
	DB 0,0,0,1,2,2,1,0,0,0
	DB 0,0,0,1,2,2,1,0,0,0
	DB 0,0,1,2,2,2,2,1,0,0
	DB 0,0,1,1,1,1,1,1,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
PRTTL:	CALL CLRSCR
	LD A,3
	CALL SPACES
	LD A,(KOLOR)
	AND A
	LD A,5
	JR NZ,$+18
	LD DE,TITLE1
	CALL PRTBLK
	LD A,1
	CALL SPACES
	LD DE,TITLE2
	JR $+16
	LD DE,TITLE2
	CALL PRTBLK
	LD A,1
	CALL SPACES
	LD DE,TITLE1
	LD A,5
	CALL PRTLIN
	RET




;
	ORG WVLMAT+300H
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0	;WHITE ROOK
	DB 0,0,0,1,1,1,1,0,0,0
	DB 0,0,0,2,2,2,2,0,0,0
	DB 0,0,0,2,2,2,2,0,0,0
	DB 0,0,0,2,2,2,2,0,0,0
	DB 0,0,0,2,2,2,2,0,0,0
	DB 0,0,0,3,3,3,3,0,0,0
	DB 0,5,5,5,5,5,5,5,5,0
	DB 0,2,2,2,2,2,2,2,2,0
;
;
;
NEWCHR:	LD HL,(0077H)
	LD DE,CHRSET
	LD BC,2048
	LDIR
	LD HL,CHRSET
	LD (0077H),HL
	LD IX,UDGDAT
	LD C,0
	LD DE,256
NC10:	LD HL,CHRSET		;points to 1st relevant byte
	LD B,0
	ADD HL,BC		;bump pointer to Cth byte
	LD B,8
NC20:	LD A,(IX+0)
	LD (HL),A
	ADD HL,DE
	INC IX
	DJNZ NC20
	INC C
	LD A,C
	CP 32			;done?
	JR NZ,NC10		;no, loop
	LD B,8
	LD HL,CHRSET+148
	LD DE,256
	LD IX,NEWDAT
NC100:	LD A,(IX+0)
	LD (HL),A
	INC IX
	ADD HL,DE
	DJNZ NC100
	RET
;
NEWDAT:	DB 00000111B
	DB 00000111B
	DB 00000111B
	DB 00000111B
	DB 00000111B
	DB 00000111B
	DB 00000111B
	DB 00000111B


;
	ORG WVLMAT+400H
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0	;WHITE QUEEN
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
CHARTR:	PUSH BC
	PUSH DE
	PUSH HL
	PUSH IX
	LD E,0
	LD A,6
	RST 32
	DB 48
CHR5:	RST 20H
	DB 38H		;ZGETKEY
	LD IX,KLRAM-2
	RST 20H
	DB 3AH		;ZKLOOK
	LD B,A
	AND 7FH
	CP 61H
	JP M,CHR10
	SUB 20H
CHR10:	CP 'A'
	JR C,CHR20
	CP 'Z'+1
	JR C,CHR30
CHR20:	CP '0'
	JR C,CHR5
	CP '9'+1
	JR NC,CHR5
CHR30:	LD A,B
	PUSH AF
	LD E,0
	RST 20H
	DB 30H		;ZOUTPUT TO CONSOLE
	LD E,0
	LD A,7
	RST 32
	DB 48
	POP AF
	POP IX
	POP HL
	POP DE
	POP BC
	AND 7FH
	CP 61H
	RET M
	SUB 20H
	RET
CLRSCR:	PUSH AF
	LD A,31		;CLEAR SCREEN
	CALL PRTCHR
	POP AF
	RET
	ORG WVLMAT+500H
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
PRTCHR:	PUSH BC
	PUSH DE
	PUSH HL
	PUSH AF
	LD E,0
	RST 20H
	DB 30H		;ZOUTPUT TO CONSOLE
POPM:	POP AF
	POP HL
	POP DE
	POP BC
	RET
PRTBLK:	PUSH BC
	PUSH DE
	PUSH HL
	LD L,0
	PUSH HL
	PUSH AF
	JR PRT1
PRTLIN:	PUSH BC
	PUSH DE
	PUSH HL
	LD L,1
	PUSH HL
	PUSH AF
PRT1:	LD A,(DE)
	INC DE
	PUSH DE
	LD E,0
	RST 20H
	DB 30H		;ZOUTPUT TO CONSOLE
	POP DE
	POP AF
	DEC A
	PUSH AF
	JR NZ,PRT1
	POP AF
	POP HL
	PUSH AF
	LD A,L
	AND A
	CALL NZ,CARRET
	JR POPM




;
;
;MAX ALLOWED INCREASE FOR ONE MOVE IS 5
;
	ORG WVLMAT+600H
BPEND:	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,14,14,14,14,14,14,14,14,0
	DB 0,10,10,10,10,10,10,10,10,0
	DB 0,6,6,6,6,6,6,6,6,0
	DB 0,3,3,3,3,3,3,3,3,0
	DB 0,1,1,1,1,1,1,1,1,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
CARRET:	PUSH AF
	LD A,28		;CURSOR HOME LEFT
	CALL PRTCHR
	LD A,13		;CURSOR DOWN WITH SCROLLING
	CALL PRTCHR
	POP AF
	RET
SPACES:	PUSH AF
	LD A,20H
	CALL PRTCHR
	POP AF
	DEC A
	JR NZ,SPACES
	RET
;
;
BEEP:	RET
;




;
	ORG BPEND+100H
WPEND:	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,1,1,1,1,1,1,1,1,0
	DB 0,3,3,3,3,3,3,3,3,0
	DB 0,6,6,6,6,6,6,6,6,0
	DB 0,10,10,10,10,10,10,10,10,0
	DB 0,14,14,14,14,14,14,14,14,0
;
;
;
INITBD:	LD B,120
	LD HL,BOARDA
	LD DE,BOARDB
IB1:	LD (HL),-1
	LD A,-1
	LD (DE),A
	INC DE	
	INC HL
	DJNZ IB1
	LD B,8
	LD IX,BOARDA
IB2:	LD A,(IX-8)
	LD (IX+21),A
	SET 7,A
	LD (IX+91),A
	LD (IX+31),PAWN
	LD (IX+81),BPAWN
	LD (IX+41),0
	LD (IX+51),0
	LD (IX+61),0
	LD (IX+71),0
	INC IX
	DJNZ IB2
	LD IX,POSK
	LD (IX+0),25
	LD (IX+1),95
	LD (IX+2),24
	LD (IX+3),94
	LD A,1
	LD (BOKFL),A
	LD (MOVENO),A
	RET



;
	ORG BPEND+200H
KEND:	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,0,0,0,0,0,0,0,0
	DB 0,0,1,1,1,1,1,1,0,0
	DB 0,0,1,2,2,2,2,1,0,0
	DB 0,0,1,2,3,3,2,1,0,0
	DB 0,0,1,2,3,3,2,1,0,0
	DB 0,0,1,2,2,2,2,1,0,0
	DB 0,0,1,1,1,1,1,1,0,0
	DB 0,0,0,0,0,0,0,0,0,0
;
; Variables start here
;
BOARDB:	DEFS 120
MVENUM:	DEFM '01 '
MVEMSG:	DEFM '   '
TT1:	DB 0
BCKSTR:	DB 16,15
BCK2:	DEFS 6
;
;
XOFF	EQU 44
YOFF	EQU 160
;
STRNSP:	DB 147,147
STRSP:	DEFM '   '
STR43:	DEFS 2
STR21:	DEFS 2
GCSTR:	DB 16
NPLOT:	DB 0
GC1:	DEFS 6
GC2:	DEFS 6
GC3:	DEFS 6
GPSTR:	DB 16
	DB 7
NCHARS:	DB 2
	DB 0
CHRSTR:	DEFS 40
;
SQCOL:	DB 0
XPOS:	DB 0
YPOS:	DB 0
;
SP4:	DB 32,32,148
NSP4:	DB 147,147,147
STR4A:	DEFS 4
STR4B:	DEFS 4
;
FAST:	DB 0
;
	DEFS 4
KLRAM:	DB 0
	DEFS 4
;
;	
	ORG ($/256+1)*256
;
CHRSET:	DEFS 2048
;


VARS	EQU 256*($/256+1)
	ORG VARS+80H
TBASE	EQU VARS+100H
DIRECT	EQU $-TBASE
	DB 9
	DB 11
	DB -11
	DB -9
	DB 10
	DB -10
	DB 1
	DB -1
	DB -21
	DB -12
	DB 8
	DB 19
	DB 21
	DB 12
	DB -8
	DB -19
	DB 10
	DB 10
	DB 11
	DB 9
	DB -10
	DB -10
	DB -11
	DB -9
DPOINT	EQU $-TBASE
	DB 20
	DB 16
	DB 8
	DB 0
	DB 4
	DB 0
	DB 0
DCOUNT	EQU $-TBASE
	DB 4
	DB 4
	DB 8
	DB 4
	DB 4
	DB 8
	DB 8
PVALUE	EQU $-TBASE-1
;
VALPCE:	DB 1,4,4,6,11,12
;
ATKLST:	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
WACT	EQU ATKLST
BACT	EQU ATKLST+7
PLIST	EQU $-TBASE-1
PLISTD	EQU PLIST+10
PLISTA:	DEFW 0,0,0,0,0,0,0,0,0,0
POSK:	DB 25
	DB 95
POSQ:	DB 24
	DB 94
	DB -1
TBASEX	EQU VARS+200H
	ORG TBASEX-8
PIECES	EQU $-TBASEX
	DB 4,2,3,5,6,3,2,4
BOARD	EQU $-TBASEX
BOARDA:	DEFS 120
	DB -1
SCORE:	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
PLYIX:	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
	DEFW 0
KLIST:	DEFS 20
KCLIST:	DEFS 20
	ORG VARS+400H
STACK:
	ORG VARS
M1:	DEFW TBASEX
M2:	DEFW TBASEX
M3:	DEFW TBASEX
M4:	DEFW TBASEX
T1:	DEFW TBASE
T2:	DEFW TBASE
T3:	DEFW TBASE
INDX1:	DEFW TBASE
INDX2:	DEFW TBASE
NPINS:	DEFW TBASE
MLPTRI:	DEFW PLYIX
MLPTRJ:	DEFW 0
SCRIX:	DEFW 0
BESTM:	DEFW 0
MLLST:	DEFW 0
MLNXT:	DEFW MLIST
KPTR:	DEFW 0 ;#0
KCAPTR:	DEFW 0 ;#0
KOLOR:	DB 0
COLOR:	DB 0
P1:	DB 0
P2:	DB 0
P3:	DB 0
PMATE:	DB 0
BOKNO:	DB 0
BOKFL:	DB 0
MOVENO: DB 0
QPLY:	DB 0
PLYMAX:	DB 0
NPLY:	DB 0
CKFLG:	DB 0
MATEF:	DB 0
STALEF:	DB 0
PRUNEF:	DB 0
PRUNCT:	DB 0
ENDF:	DB 0
CUMMTL:	DB 0
NOQENF:	DB 0
VALM:	DB 0
BRDC:	DB 0
BRDP:	DB 0
BP0:	DB 0
PTSL:	DB 0
PTSW1:	DB 0
PTSW2:	DB 0
MTRL:	DB 0
BC0:	DB 0
MV0:	DB 0
PTSCK:	DB 0
BESTF:	DB 0
BLANKF:	DB 0
STOPF:	DB 0
;
;
	ORG VARS+400H
SRTATN:	DEFS 120
	ORG VARS+500H
MINATW:	DEFS 120
	ORG VARS+600H
MINATB:	DEFS 120
;
	ORG 256*($/256+1)
MLIST:	DEFS 2048
MLEND	EQU MLIST+2040
MLPTR	EQU 0
MLFRP	EQU 2
MLTOP	EQU 3
MLFLG	EQU 4
MLVAL	EQU 5
BRDPOS:	DEFS 1
ANBDPS:	DEFS 1
NORMAD:	DEFS 2
LINECT:	DEFS 1 ;0
MATLIM	EQU 14
POSLIM	EQU 12

	END